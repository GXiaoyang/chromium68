# Copyright (c) 2018 LG Electronics, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

import("//build/buildflag_header.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/features.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build/config/ui.gni")

declare_args() {
  use_neva_app_shell = false
}

copy("cursor") {
  sources = [ "images/cursor.png" ]
  outputs = [ "$root_out_dir/cursor.png" ]
}

source_set("wam_demo_lib") {
  testonly = !use_cbe

  configs += [
    "//content:content_implementation",
    "//v8:external_startup_data",
  ]

  sources = [
    "util.cc",
    "util.h",
    "wam_demo_main_delegate.cc",
    "wam_demo_main_delegate.h",
    "wam_demo_service.cc",
    "wam_demo_service.h",
    "wam_demo_webapp.cc",
    "wam_demo_webapp.h",
  ]

  deps = [
    ":cursor",
    "//neva/app_runtime",
    "//base",
    "//base:base_static",
    "//neva/emulator",
  ]

  if (use_neva_app_shell) {
    deps += [
      "//neva/neva_shell:neva_app_shell_lib",
    ]

    defines = [
      "USE_NEVA_APP_SHELL",
    ]
  }

  include_dirs = [ "." ]
}

executable("wam_demo") {
  testonly = true
  output_name = "wam_demo"

  configs += [
    "//build/config:precompiled_headers",
    "//content:content_implementation",
  ]

  sources = [
    "wam_demo.cc",
  ]

  deps = [
    "//third_party/boringssl",
  ]

  if (use_cbe && is_wam_demo_cbe) {
    deps += [
      "//neva/cbe:chromium_cbe"
    ]
  } else {
    deps += [
      ":wam_demo_lib",
    ]
  }
}
